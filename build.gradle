plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group = 'dev.root101.commons'
version = '5.1.12.RELEASE.20240724'

def environment = System.getenv('ENVIRONMENT') ?: 'prod'

repositories {
    jcenter()
    mavenCentral()
}

dependencies{
    //validation
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    //runtime de validation, si no da error:
    //jakarta.validation.NoProviderFoundException: Unable to create a Configuration, because no Jakarta Bean Validation provider could be found. Add a provider like Hibernate Validator (RI) to your classpath.
    implementation 'org.hibernate.validator:hibernate-validator:8.0.0.Final'

    //spring
    implementation 'org.springframework.boot:spring-boot-starter-web:3.0.5'

    //Jackson
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.14.2'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Root-101/java-commons")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "Java-Commons"
                description = "Basic common java utilities for easy work in backend"
                url = "https://github.com/Root-101/java-commons"
                properties = ['environment': environment]
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id = "root-101"
                        name = "Root-101"
                        email = "jhernandezb96@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com:Root-101/java-commons.git"
                    developerConnection = "scm:git:ssh://github.com:Root-101/java-commons.git"
                    url = "https://github.com:Root-101/java-commons"
                }
            }
        }
    }
}